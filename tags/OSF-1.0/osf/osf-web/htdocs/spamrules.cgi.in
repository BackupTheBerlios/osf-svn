#! @PERL@

BEGIN {
    unshift @INC, '@datadir@';
}

use strict;
no strict qw(refs);
use warnings;
use English;

use CGI qw(:standard);

use OSF::HTML;
use OSF::Config;
use OSF::Validate;

use Template;

my $SA_RULE_DEFAULT_SCORE = 3.7;

my $template = Template->new( { INCLUDE_PATH => "@datadir@/templates/htdocs" } );


sub print_config_form($)
{
    my ($cgi) = @_;
    my $self = $cgi->script_name();

    html_top($cgi);

    my %config = read_config_file();
    my @rules = ();
    if (defined($config{'spamrules'})) {
	@rules = @{$config{'spamrules'}};
    }

    $template->process('spamrules',
		       {
			rules => \@rules,
			self => $self
		       } )
      or die $template->error();

    html_bottom();
}


sub form_to_config($%)
{
    my ($cgi, %config) = @_;
    my %newrules;

    foreach my $param ($cgi->param) {
	my ($type, $key) = ($param =~ /(from|to|subject|body|score)_(\w+)/) or next;
	$newrules{$key}->{$type} = $cgi->param($param) unless $cgi->param("delete_$key");
    }

    my @filtered;
    foreach (sort keys %newrules) {
	my $rule = $newrules{$_};

	# do not write empty rule
	next if ($rule->{from} eq '' && $rule->{to} eq '' && $rule->{subject} eq '' && $rule->{body} eq '');

	foreach my $field ('from', 'to', 'subject', 'body') {
	    if ($rule->{$field} ne '') {
		if (!is_valid_regexp($rule->{$field})) {
		    $template->process('common_formerror', { 'field' => $_, 'value' => $rule->{$field} });
		    return ();
		}
	    }
	}

	if (!defined($rule->{score}) || $rule->{score} eq '') {
	    $rule->{score} = $SA_RULE_DEFAULT_SCORE;
	}

	# 4,5 -> 4.5
	$rule->{score} =~ s/,/\./g;

	if (!is_valid_sa_score($rule->{score})) {
	    $template->process('common_formerror', { 'field' => $_, 'value' => $rule->{score} });
	    return ();
	}

	push @filtered, $rule;
    }

    $config{'spamrules'} = \@filtered;

    return %config;
}


sub do_submit($)
{
    my ($cgi) = @_;
    my $self = $cgi->script_name();
    my %config;

    html_top($cgi);

    %config = read_config_file();
    %config = form_to_config($cgi, %config) or return;
    eval {
	write_config_file(%config);
    };
    $template->process('common_submitresponse', { error => $@,
						  self => $self });

    html_bottom();
}


#
# Main program
#

my $cgi = new CGI();

if ($cgi->param("submit")) {
    do_submit($cgi);
} else {
    print_config_form($cgi);
}

exit 0;
